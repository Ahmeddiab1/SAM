////////////////////////////////////////////////////////////////////////////////
//
// This script demonstrates the use of the webapi() function to access the APIs
// SAM uses to download data from online databases and for other functions.
//
// The webapi() function returns a list of URLS stored in the webapis.conf text
// file in the SAM runtime folder.
//
// Written in SAM 2018.11.11, January 2019
//
////////////////////////////////////////////////////////////////////////////////

outln( 'SAM runtime folder: ' + runtimedir() );
outln();

outln( 'List of APIs defined in webapis.conf file:');
apis = webapi();
outln( apis );
outln();

// the nsrdb_query variable and its URL value are defined in the webapis.conf file
// this example shows how to get the URL value from the webapis.conf file and
// then use the URL to query the database
outln( 'Get URL for biomass resource data using webapi() function:' );
biomassresource = webapi( 'biomass_resource' );
outln( biomassresource );
outln();

// url is the value of nsrdb_query from the webapis.conf file
outln( 'Use webapi() to get nsrdb_query URL:' );
url = webapi( 'nsrdb_query' );
outln( url );
outln();

// replace placeholders in url with valid values 
outln( 'URL for NSRDB query with placeholders replace by valid values: ' );
lat = 39.7;
lon = -105.2;
url = replace( url, '<LAT>', lat );
url = replace( url, '<LON>', lon );
// curl() replaces <SAMAPIKEY> with SAM's built-in API key
// you can also get your own key at https://developer.nrel.gov and use replace()
// to replace <SAMAPIKEY> with your own key.
json_str = curl( url );
outln( url );
outln();

outln( 'JSON string returned by NSRDB query:' );
outln( json_str );

// json_read() converts a JSON string into a table
outln( 'JSON string converted to a variable by json_read() function' );
var = json_read( json_str );
outln( var );
