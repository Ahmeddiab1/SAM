/*@

<p>This macro converts a solar resource file from the TMY2, TMY3, EPW, or SolarAnywhere&reg; 
format to the SAM CSV format. These text file formats were orginally developed for 
the datasets described below.</p>
<br><br>
<ul>
<li><b>TMY2</b> is a non-delimited format developed by NREL for the National 
    Solar Radiation database for the 1961-1990 update.</li>
<li><b>TMY3</b> is a comma-delimited format developed by NREL for the NSRDB
    1991-2010 update.</li>
<li><b>EPW</b> is a comma-delimited format developed for the U.S. Department
    of Energy's EnergyPlus building simulation software.</li>
<li><b>PVsyst CSV</b> is a comma-delimited format used by PVsyst.</li>
<li><b>SolarAnywhere</b> is a comma-delimited format used by Clean Power 
    Research's weather data service.</li>
</ul>

<p>Although SAM can read files in the TMY2, TMY3 and EPW format, those files
contain extra information about uncertainties and other values that SAM does not
use. They are also difficult to work with in spreadsheet software. You can use
this macro to convert those files to a "stripped" down copy with only the 
data that SAM uses in a file that you can edit or view in a spreadsheet program.</p>

<p>For more information about weather data for SAM, see the
<a href="https://sam.nrel.gov/weather-data"</a>Weather Data page</a> on the SAM
website.</p>

<p><b>To convert a file:</b></p>
<ol>
<li>Choose a format from the list at right.</li>
<li>Click <b>Run macro</b> above and choose the weather file to convert.</li>
</ol>

<p>The macro will attempt to convert the file you open and save the converted
data in a SAM CSV file in the same folder as the original file with the name 
<i>&lt;country> &lt;location> (&lt;original format>).csv</i>. It will generate 
an error message if it does not recognize the file format or encounters invalid 
data during the conversion process.</p>

@*/

// Macro user interface widgets
//@ name=type;type=combo;value=TMY2,TMY3,EPW,PVsyst CSV,SolarAnywhere;label=Weather file format;

if ( typeof(macro) == 'unknown' ) {
	msgbox('This macro must be run from within a case.');
	exit;
}

format = macro.type;
if ( format == '' ) 
{
  msgbox('Please choose a weather file format!\nChoose TMY2, TMY3, EPW, PVsyst CSV or SolarAnywhere from the list at right, and then run the macro again.');
  exit;
}  

wf = choose_file( '', 'Choose weather file to convert', 'Weather Files (*.csv;*.tm2;*.epw)|*.csv;*.tm2;*.epw' );
if ( wf == '' ) return;

if (format != 'SolarAnywhere' && format != 'PVsyst CSV') 
{
	obj = ssc_create();
	ssc_var( obj, 'input_file', wf );
	result = ssc_exec( obj, 'wfcsvconv', true );
	if ( result == 0 )
	{
		output = ssc_var( obj, 'output_file');
		if ( file_exists(output) )
		{
			outln('Weather file successfully converted.');
			browse( path_only(output) ); // show output file folder
			dview_solar( output ); // display output file in DView
		}
		else
		{
			outln('An error occurred and the file could not be converted.');
		}
	}
	else
		msgbox( 'Error:\n\n' + result );
	ssc_free( obj );
}
else // SolarAnywhere or PVsyst CSV
{
	// get input file and open it
	input_file = wf;
	path = path_only( input_file );
	outln("Working folder is \n  " + path);
	input_name = file_only( input_file );
	fin = 0;
	fout = 0;
	fin = open( input_file , 'r' );
	if ( fin == 0 ) {
		outln('Could not open ' + input_name + ' for reading.\nExiting script.');
		exit;
	}
	else {
		outln('File to convert is\n  ' + input_name + '\n');
	}
}

if ( format == 'SolarAnywhere' ) 
{
	// get location year from input file for the output file name
	// (need year in case we need it for multi-year input file)
	line = 0;
	ok = true;
	str_line = '';
	while ( read_line( fin, str_line ) && ok == true ) 
	{
		if ( line == 0 ) 
		{
			arr = split( str_line, ',');
			if ( #arr != 8 ) 
			{
				msgbox('Input file does not appear to be in a SolarAnywhere format: First row has ' + #arr + ' columns instead of 8. Exiting script.\n');
				exit;
			}
			flocation = arr[1];
			fstate = arr[2];
		}
		if ( line == 2 ) 
		{
			arr = split( str_line, ',');
			arr = split( arr[0], '/');
			fyear1 = arr[0];
			ok = false;
		}  
		line++;
	}
	
	seek(fin,0,0); // move position in input file back to beginning

	// open output file for writing using location from input file in name
	str = split( file_only(input_file), '.' );
	out_name = '/' + str[0] + '_samcsv';
	output_file = path + out_name + '.csv';
	fout = open( output_file , 'w' );
	if ( fout == 0 ) 
	{
		msgbox('Could not open ' + output_file + ' for writing. Exiting script.');
		exit;
	}
	else
	{
		outln('Converted data will be stored in\n ' + file_only(output_file) + '\n');
	}	

	missing = -999;
	str_line = '';
	line = 0;
	ok = true;
	feb29 = false;
	fcount = 0;
	outln('Converting file, please wait...');
	// read input file line by line and write to output file as we go
	while ( read_line( fin, str_line ) ) 
	{
		// read header data from first row of input file. 
		// SA and SA TMY3 format use same header format
		// write first and second header rows to output file
		if ( line == 0 ) 
		{
			headers = 'Source,Location ID,City,State,Country,Latitude,Longitude,Time Zone,Elevation';
			ok = write_line( fout , to_string(headers) );
			if ( ok == false ) 
			{
				outln('Failed to write column headers to file. Exiting script.\n');
				exit;
			}
			arr = split( str_line, ',' );
			if ( #arr != 8 ) 
			{
				outln('Input file does not appear to be in a SolarAnywhere format: First row has ' + #arr + ' columns instead of 8. Exiting script.\n');
				exit;
			}
			h1[0] = "SolarAnywhere"; // source
			h1[1] = arr[0]; // location
			cs = split(arr[1],'-');
			city_state = split(cs[0],' ');
			state = city_state[#city_state-1];
			city_state -@ #city_state-1;
			city = join(city_state,' ');
			h1[2] = city; // city
			h1[3] = state; // region
			h1[4] = 'Unknown'; // country
			h1[5] = arr[4]; // latitude
			h1[6] = arr[5]; // longitude
			h1[7] = arr[3]; // time zone
			h1[8] = arr[6]; // elevation
			ok = write_line( fout , to_string(h1) );
			if ( ok == false ) 
			{
				outln('Failed to write first header row to file. Exiting script.\n');
				exit;
			}
		}
		// skip reading second row of input file that contains column headers
		// write third header row to output file
		elseif ( line == 1 ) 
		{
			column_headers = 'Year,Month,Day,Hour,GHI,DNI,DHI,Tdry,Tdew,RH,Pres,Wspd,Wdir,Albedo';
			ok = write_line( fout , to_string(column_headers) );
			if ( ok == false ) 
			{
				outln('Failed to write column headers to file. Exiting script.\n');
				exit;
			}
		}
		// convert data row from input file
		// write converted data row to output file
		else 
		{
			// replace empty data with -999 missing data flag
			data_str = '';
			for ( i=0; i<strlen(str_line); i++ ) 
			{
				ch_i = mid(str_line, i, 1);
				if ( ch_i == ',' && mid(str_line, i+1, 1) == ',' ) 
				{
					data_str = data_str + ch_i + to_string(missing);
				}	
				else
				{
					data_str = data_str + ch_i;
				}	
			}
			// convert comma-separated row to an array
			arr = split( data_str, ',');
			// if less than 15 columns, assume format is SA 
			// number of columns varies between "time series" (12) and "average" (11) files
			// TMY3 format has 68 columns 
			if (#arr < 15 ) 
			{
				format = 'SolarAnywhere';
				datetime = split( arr[0] , ' ');
				date = split( datetime[0] , '/');
				hour = split( datetime[1] , ':');
				y = to_int(date[0]);
				m = to_int(date[1]);
				d = to_int(date[2]);
				h = to_int(hour[0]);
				data[0] = to_string(y); // year
				data[1] = to_string(m); // month
				data[2] = to_string(d); // day
				data[3] = to_string(h); // hour    
				data[4] = arr[1]; // ghi
				data[5] = arr[2]; // dni
				data[6] = arr[9]; // dhi
				data[7] = arr[3]; // tdry
				data[8] = missing; // tdew
				data[9] = missing; // rh
				data[10] = missing; // pres
				data[11] = arr[4]; // wspd
				data[12] = missing; // wdir
				data[13] = missing; // alb
			}
			// if not 12 columns, assume format is SA TMY3
			else 
			{
				format = 'SolarAnywhere TMY3';
				date = split( arr[0] , '/');
				hour = split( arr[1] , ':');
				y = to_int(date[2]);
				m = to_int(date[0]);
				d = to_int(date[1]);
				h = to_int(hour[0]);
				data[0] = to_string(y); // year
				data[1] = to_string(m); // month
				data[2] = to_string(d); // day
				data[3] = to_string(h); // hour    
				data[4] = arr[4]; // ghi
				data[5] = arr[7]; // dni
				data[6] = arr[10]; // dhi
				data[7] = arr[31]; // tdry
				data[8] = arr[34]; // tdew
				data[9] = arr[37]; // rh
				data[10] = arr[40]; // pres
				data[11] = arr[46]; // wspd
				data[12] = arr[43]; // wdir
				data[13] = arr[61]; // alb
			}
			//convert time stamp from hour ending to hour beginning
			//and move last time stamp from Jan 1 to Dec 31
			h = h - 1;
			if ( h == -1 ) 
			{
				h = 23;
				if ( m == 1 && line > 2 ) 
				{
					y = y - 1;
					m = 12;
					d = 31;
				}
			}
			feb29 = ( m == 2 && d == 29 ); // feb29 is true for leap years
			// check to see if input file is multi-year file
			// if it is, close the output file and append year to file name 
			// then, open a new output file, and write header data to it
			if ( fcount != to_int( (line-2)/8760 ) ) 
			{
				close(fout);
				// if this is the first file in the multi-year series, rename it
				if (fcount == 0) 
				{ 
					outln('\nMulti-year input file detected. Converting to separate files...');
					new_name = replace( output_file, '_samcsv',  '_samcsv_' + fyear1 );
					ok = rename_file( output_file , new_name );
					if (ok == true )
					{
						outln('    Year 1 file renamed to\n     ' + output_file );
					}	
					else
					{
						outln('  Failed to rename Year 1 output file');
					}	
				}
				output_file = replace( input_file, '.csv',  '_samcsv_' + y + '.csv');
				fout = open( output_file , 'w' );
				write_line( fout , to_string(headers) );
				write_line( fout , to_string(h1) );
				write_line( fout , to_string(column_headers) );
				fcount++;
				outln('    Saving data for year ' + y + ' in\n     ' + file_only(output_file) );
			}
			// write converted data row to output file, skipping February 29 for leap years
			if ( feb29 == false ) 
			{
				ok = write_line( fout , to_string(data) );
			}  
			if ( ok == false ) 
			{
				outln('Failed to write data row ' + line + ' to file.\n');
				exit;
			}
		}  
		if ( feb29 == false ) line++;
	}

	// close input and output files
	close(fout);
	close(fin);

	// check to see if input file was for a set of complete years
	// if not, rename the output file with a partial year
	// (assumes that partial year was the last year in the multi-year file)
	if ( mod(line-2,8760)!=0 ) 
	{ // line should be the total number of lines in the input file
		outln('\n'+file_only(output_file) + ' does not contain a complete year of data.');
		new_name = replace( output_file, '_samcsv_' + to_string(y), '_samcsv_' + to_string(y) + 'part' );
		ok = rename_file( output_file , new_name );
		if (ok==true)
		{
			outln('File renamed to\n ' + file_only(output_file) );
		}
		else
		{
			outln('Unable to rename ' + file_only(output_file) );
		}
	}

	outln('Weather file successfully converted.');
	browse( path_only(output_file) ); // pop up folder where file exists
	// if input file is for a single year, show converted data in data viewer
	if (fcount == 0 )
	{
		//display_data( output_file );
		dview_solar( output_file );
	} 
} // end if SolarAnywhere

if ( format == 'PVsyst CSV' )
{
	outln('Converting file, please wait...');
	str_line = '';
	ts = 0;
	while ( read_line( fin, str_line ) ) 
	{
		if ( strpos( str_line, '#') > - 1 )
		{
			arr = split( str_line , ',' );
			if ( #arr > 1 )
			{
				if ( strpos( lower(arr[0]) , 'lat' ) > -1 ) { Header{'Latitude'} = arr[1]; }
				if ( strpos( lower(arr[0]) , 'lon' ) > -1 ) { Header{'Longitude'} = arr[1]; }
				if ( strpos( lower(arr[0]) , 'alt' ) > -1 ) { Header{'Elevation'} = arr[1]; }
				if ( strpos( lower(arr[0]) , 'zone' ) > -1 ) { Header{'Time Zone'} = arr[1]; }
			}
		}
		if ( strpos( lower(str_line), 'year' ) > -1 )
		{
			if ( strpos( str_line , 'WindVel' ) > -1 ) { replace( str_line, 'WindVel', 'Wspd' ); }
			keys = split( str_line, ',' );
		}
		if ( strpos( lower(str_line), 'w/m2' ) == -1 && strpos( str_line, '#' ) == - 1 )
		{
			arr = split( str_line, ',' );
			for ( i=0; i<#arr; i++ )
			{
				Data{keys[i]}[ts] = arr[i];
			}
			ts++;
		}
	}
	close( fin );

	output_file = replace( input_file, '.csv' , '_samcsv.csv' );
	str = '';

	keys = @Header;
	for ( i=0; i<#keys; i++ )
	{
		str += keys[i];
		if ( i<#keys-1 ) { str += ','; }
		else { str += '\n'; }
	}
	for ( i=0; i<#keys; i++ )
	{
		str += Header{keys[i]};
		if ( i<#keys-1 ) { str += ','; }
		else { str += '\n'; }
	}

	keys = @Data;
	for ( i=0; i<#Data{keys[0]}; i++ )
	{
		for ( j=0; j<#keys; j++ )
		{
			str += Data{keys[j]}[i];
			if ( j<#keys-1 ) { str += ','; }
			else { str += '\n'; }
		}
	}

	write_text_file( output_file, str );
	dview_solar( output_file );
	browse( path_only( output_file ) );

}

outln("Done.");

